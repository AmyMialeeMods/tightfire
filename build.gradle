plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.7.4"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name + "-" + project.minecraft_version
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

loom {
    accessWidenerPath = file("src/main/resources/tightfire.accesswidener")
}

publishMods {
    file = remapJar.archiveFile
    additionalFiles.files.add(remapSourcesJar.archiveFile.get().getAsFile())
    displayName = "${project.version.toString().split("\\+")[0]} for Fabric $minecraft_version"
    changelog = ""
    type = STABLE
    modLoaders.addAll("fabric", "quilt")
    dryRun = !providers.gradleProperty("curseforge_api_key").isPresent() || !providers.gradleProperty("modrinth_api_key").isPresent() || !providers.gradleProperty("discord_webhook").isPresent()
    curseforge {
        accessToken = providers.gradleProperty('curseforge_api_key')
        projectId = curseforge_id
        minecraftVersionRange {
            start = minecraft_version_range_start
            end = minecraft_version_range_end
        }
        announcementTitle = "Download from CurseForge"
        javaVersions.addAll(JavaVersion.VERSION_21, JavaVersion.VERSION_22)
        clientRequired = true;
        serverRequired = true;
        projectSlug = curseforge_slug
    }
    modrinth {
        accessToken = providers.gradleProperty("modrinth_api_key")
        projectId = modrinth_id
        minecraftVersionRange {
            start = minecraft_version_range_start
            end = minecraft_version_range_end
            includeSnapshots = true
        }
        announcementTitle = "Download from Modrinth"
    }
//    discord {
//        webhookUrl = providers.gradleProperty("discord_webhook")
//        dryRunWebhookUrl = providers.gradleProperty("discord_webhook")
//        username = "Abee"
//        avatarUrl = "https://cdn.discordapp.com/emojis/1006355140657299528.gif?quality=lossless"
//        setPlatforms(publishMods.platforms.modrinth, publishMods.platforms.curseforge)
//    }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(21)
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

jar {
    from("LICENSE.md") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}